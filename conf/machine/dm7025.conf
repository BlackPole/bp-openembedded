#@TYPE: Machine
#@NAME: Dreambox DM 7025 PVR
#@DESCRIPTION: Machine configuration for the Dreambox DM 7025 PVR

#take care when you do changes on MACHINE_ESSENTIAL_EXTRA_RDEPENDS/RRECOMMENDS you have to increment the recipes/tasks/task-boot.bb PR

# No 2.6.18 kernel for the 7025
PREFERRED_VERSION_linux-${MACHINE} = "2.6.32"
PREFERRED_VERSION_linux-libc-headers = "2.6.32"

# klibc 1.5.18 doesn't build for kernel 2.6.32, the .15 is patched to work...
PREFERRED_VERSION_klibc = "1.5.15"

EXTRA_IMAGEDEPENDS += "\
        klibc \
	squashfs-tools-native \
        dreambox-boottool \
"

MACHINE_ESSENTIAL_EXTRA_RDEPENDS = "\
        kernel-module-pata-xilleon \
        kernel-module-nls-iso8859-15 \
        kernel-module-nls-iso8859-1 \
        kernel-module-nls-cp437 \
        kernel-module-nls-cp850 \
        kernel-module-nls-utf8 \
        kernel-module-squashfs \
        kernel-module-unionfs \
        kernel-module-loop \
        dreambox-boottool \
"

module_autoload_stv0299 = "stv0299"
module_autoload_nls-iso8859-1 = "nls-iso8859-1"
module_autoload_nls-iso8859-15 = "nls-iso8859-15"
module_autoload_nls-cp437 = "nls-cp437"
module_autoload_nls-cp850 = "nls-cp850"
module_autoload_nls-utf8 = "nls-utf8"

MACHINE_ESSENTIAL_EXTRA_RRECOMMENDS = "\
	kernel-module-cifs \
	kernel-module-exportfs \
	kernel-module-ext2 \
	kernel-module-xfs \
	kernel-module-iso9660 \
	kernel-module-udf \
	kernel-module-cdfs \
"

module_autoload_cifs = "cifs"
module_autoload_exportfs = "exportfs"
module_autoload_ext2 = "ext2"
module_autoload_xfs = "xfs"
module_autoload_iso9660 = "iso9660"
module_autoload_udf = "udf"
module_autoload_cdfs = "cdfs"
module_autoload_ntfs = "ntfs"

MACHINE_EXTRA_RRECOMMENDS = " \
	gst-plugin-dvbmediasink \
	dreambox-tpmd \
	dreambox-keymaps \
	"

#KERNEL_EXTRA_CMD = "--disable-compressor=lzo "

# A list of files/dirs that should be in uncompressed space
DONT_SQUASH_LIST = "usr/lib/ipkg/status home media mnt hdd etc/tuxbox"


EXTRA_IMAGECMD_jffs2 = " --eraseblock=0x4000 -n -l "

_squash_IMAGE_CMD_jffs2 = "\
                install -d ${DEPLOY_DIR_IMAGE}/boot_tmp; \
                for i in bin dev mnt/flash mnt/squashfs mnt/root; \
                do \
                        install -d ${IMAGE_ROOTFS}/boot/$i; \
                done; \
                install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/fs; \
                install -d ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/drivers/block; \
                mv ${IMAGE_ROOTFS}/lib/modules/${KERNEL_VERSION}/kernel/fs/squashfs \
                        ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/fs; \
                mv ${IMAGE_ROOTFS}/lib/modules/${KERNEL_VERSION}/kernel/fs/unionfs.ko \
                        ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/fs; \
                mv ${IMAGE_ROOTFS}/lib/modules/${KERNEL_VERSION}/kernel/drivers/block/loop.ko \
                        ${IMAGE_ROOTFS}/boot/lib/modules/${KERNEL_VERSION}/kernel/drivers/block; \
                mkfs.jffs2 --root=${IMAGE_ROOTFS}/boot --faketime \
                        --output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
                        ${EXTRA_IMAGECMD}; \
                mv ${IMAGE_ROOTFS}/boot/* ${DEPLOY_DIR_IMAGE}/boot_tmp; \
                \
                install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta; \
                cd ${IMAGE_ROOTFS}; \
                for i in ${DONT_SQUASH_LIST} ; \
                do \
                        j=`dirname $i` ; \
                        install -d ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${j}; \
                        mv $i ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/${j}; \
                done; \
                cat ${IMAGE_ROOTFS}/etc/fstab | sed 's/^\/dev\/mtdblock\/2/\/dev\/root/;' > ${IMAGE_ROOTFS}/etc/fstab_neu; \
                mv -f ${IMAGE_ROOTFS}/etc/fstab_neu ${IMAGE_ROOTFS}/etc/fstab; \
                mksquashfs ${IMAGE_ROOTFS}/* ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/squashfs \
                        -root-owned -le -noappend; \
                \
                mkfs.jffs2 --root=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME} --faketime \
                        --output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
                        ${EXTRA_IMAGECMD}; \
                \
                for i in ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}/delta/*; \
                        do cp -R $i ${IMAGE_ROOTFS}; done; \
                rm -rf ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}; \
                rm -rf ${IMAGE_ROOTFS}/boot; \
                mv ${DEPLOY_DIR_IMAGE}/boot_tmp ${IMAGE_ROOTFS}/boot; \
        buildimage ${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
                ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
                ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
                ${MACHINE} \
                > ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"

IMAGE_CMD_jffs2 = "\
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS}/boot \
		--faketime \
		--disable-compressor=lzo \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${EXTRA_IMAGECMD}; \
	rm -rf ${IMAGE_ROOTFS}/boot/*; \
	mkfs.jffs2 \
		--root=${IMAGE_ROOTFS} \
		--faketime \
		--compression-mode=size \
		--output=${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${EXTRA_IMAGECMD}; \
	buildimage ${STAGING_LIBDIR}/dreambox-secondstage/main.bin.gz \
		${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.boot.jffs2 \
		${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.rootfs.jffs2 \
		${MACHINE} \
		> ${DEPLOY_DIR_IMAGE}/${IMAGE_NAME}.nfi"


TARGET_FPU = "soft"

MACHINE_FEATURES += "usbhost"

# openpli specific features:
MACHINE_FEATURES += "32bpp switchoff lpcm dvb-c frontprocessor"

require conf/machine/include/dreambox-mipsel.inc
include conf/machine/include/autoload-usbserial.inc

# No 2.6.18 kernel for the 7025
PREFERRED_VERSION_linux-${MACHINE} = "2.6.32"
PREFERRED_VERSION_linux-libc-headers = "2.6.32"

