DESCRIPTION = "DSSSL stylesheets used to transform SGML and XML DocBook files"

# Simple persmissive
LICENSE = "DSSSL"

DEPENDS = "sgml-common-native"

SRC_URI = "${SOURCEFORGE_MIRROR}/docbook/docbook-dsssl-${PV}.tar.bz2"

S = "${WORKDIR}/docbook-dsssl-${PV}"

PR = "r1"

SYSROOT_PREPROCESS_FUNCS += "docbook_dssl_stylesheets_native_mangle"

inherit native

do_install () {
    # Refer to http://www.linuxfromscratch.org/blfs/view/stable/pst/docbook-dsssl.html
    # for details.
    install -d ${D}${bindir}
    install -m 0755 bin/collateindex.pl ${D}${bindir}
     
    install -d ${D}${datadir}/sgml/docbook/dsssl-stylesheets-${PV}
    install -m 0644 catalog ${D}${datadir}/sgml/docbook/dsssl-stylesheets-${PV}
    cp -PpRr common ${D}${datadir}/sgml/docbook/dsssl-stylesheets-${PV}

    install-catalog --add ${sysconfdir}/sgml/dsssl-docbook-stylesheets.cat \
      ${D}${datadir}/sgml/docbook/dsssl-stylesheets-${PV}/catalog

    install-catalog --add ${sysconfdir}/sgml/dsssl-docbook-stylesheets.cat \
      ${D}${datadir}/sgml/docbook/dsssl-stylesheets-${PV}/common/catalog

    install-catalog --add ${sysconfdir}/sgml/sgml-docbook.cat \
      ${sysconfdir}/sgml/dsssl-docbook-stylesheets.cat

    # Moving them to image directory so that they get included in the .ipk file 
    # generated by new style staging.
    install -d ${D}${sysconfdir}/sgml
    cp ${sysconfdir}/sgml/dsssl-docbook-stylesheets.cat ${D}${sysconfdir}/sgml/
    cp ${sysconfdir}/sgml/sgml-docbook.cat ${D}${sysconfdir}/sgml/
    cp ${sysconfdir}/sgml/catalog ${D}${sysconfdir}/sgml/
}

PACKAGES = ""

docbook_dssl_stylesheets_native_mangle () {
    # Removing the image directory path (${D}) from the .cat file.
    sed -i -e "s|${D}||g" ${SYSROOT_DESTDIR}${sysconfdir}/sgml/dsssl-docbook-stylesheets.cat
}

SRC_URI[md5sum] = "bc192d23266b9a664ca0aba4a7794c7c"
SRC_URI[sha256sum] = "2f329e120bee9ef42fbdd74ddd60e05e49786c5a7953a0ff4c680ae6bdf0e2bc"

NATIVE_INSTALL_WORKS = "1"
